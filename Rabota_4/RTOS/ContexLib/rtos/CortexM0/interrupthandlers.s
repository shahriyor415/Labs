// Filename: interrupthandler.s
// Created by by Sergey aka Lamerok on 14.03.2020.

  RSEG CODE:CODE:NOROOT(2)

  PUBLIC  HandlePendSv
  PUBLIC  HandleSvc

  EXTERN  Schedule

; Исключение выполняется в Hanlder режми, но планировщик должен запуcкаться в
; Thread режиме. Этот обработчик резервирует кадр исключения и переключается в
; Thread режим через этап выход из исключения для перехода в Schedule функцию
HandlePendSv: 
  ; Очищаем бит запроса на PendSv, путем установки PendSvClr бита(#27) в ICSR регистре
  ; Адрес ICSR(Interrupt Control and State Register) регистра 0xE000ED04
  LDR     r3,=0xE000ED04
  LDR     r1,=1<<27
  ; Запрещаем все прерывания
  CPSID   i
  ; Собственно скидываем бит PendSvClr
  STR     r1,[r3]
  ; Когда мы вернемся из прерывания XPSR должен стоять T - bit(1 << 24), говорящий
  ; о том, что у нас Thumb набор команд, а то если он не будет стоять, исполнение
  ; команд накроется медным тазом.
  LDR     r3,=1<<24           
  ; Когда мы вернемся из прерывания, мы должны попасть в Schedule, поэтому в PC должен
  ; быть записан адрес функции Schedule и он должен быть четным
  LDR     r2,=Schedule - 1
  ; А завершив функцию Schedule мы должны вернуться по адресу ScheduleReturn
  ; Запишем этот адрес в LR
  LDR     r1,=ScheduleReturn
  ; Теперь резервируем кадр исключения для выхода из исключения
  SUB     sp,sp,#8*4            
  ADD     r0,sp,#5*4
  ; И сохраняем в него новые XPSR, PC, LR
  STM     r0!,{r1-r3}           
  ; r0 = 0xFFFFFFF9 - Thread режим и используем MSP стек
  LDR     r0,=0xFFFFFFF9              
  ; Выходим из исключения со значением 0xFFFFFFF9 и попадаем после этого в Schedule
  BX      r0

; Вовзращаемся сюдя из функции Schedule, разрешаем прерывание и
; и вызываем исключение SVC, чтобы вернуться в вытесненную задачу
ScheduleReturn:
  CPSIE   i
  ; SVC будет выполнен вмест с CPSIE, так как Cortext M0 имеет вдухстадийный конвейер.
  SVC #0
  
; Собственно после запроса на SVC исключение попадаем сюда
HandleSvc:
  ; Удаляем кадр исключения который мы добавили в PendSV,
  ; нам нужно оставить только кадр исключения от вытесненной задачи
  ADD     sp,sp,#(8*4)
  ; Возвращаемся в вытесненную задачу
  BX      lr
  END